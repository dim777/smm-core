plugins {
    id 'java'
    id 'nu.studer.jooq' version "$jooqGenerator"
}

dependencies {
    implementation libraries.gson
    implementation libraries.jooq
    runtimeOnly libraries.postgresql
    jooqGenerator libraries.postgresql
}

jooq {
    version = versions.jooqVersion
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/postgres'
                    user = 'postgres'
                    password = 'pass'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'sm'
                        forcedTypes {
                            forcedType {
                                userType = 'com.google.gson.JsonElement'
                                binding = 'tech.ineb.sport.lib.common.models.utils.PostgresJSONGsonBinding'
                                includeExpression = '.*'
                                includeTypes = '.*jsonb.*'
                            }
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'INET'
                            }
                        }
                        recordVersionFields = 'version'
                    }
                    generate {
                        deprecated = false
                        daos = true
                        records = true
                        pojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'tech.ineb.sport.lib.common.models'
                        directory = "$buildDir/generated/jooq/src/main/java"
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

sourceSets {
    main {
        java.srcDirs += ["${buildDir}/generated/jooq/src/main/java"]
    }
}
